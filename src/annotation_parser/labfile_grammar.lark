labfile: line (_WHITES line)* _WHITES
line: FLOAT _WHITES FLOAT _WHITES chord
chord:    pitchname ":" SHORTHAND [ "(" components ")" ] [ "/" interval ]
        | pitchname ":" "(" components ")" [ "/" interval ]
        | pitchname [ "/" interval ]
        | NO_CHORD
pitchname: NATURAL MODIFIER*
interval: MODIFIER* INTEGER
interval_to_add: interval
interval_to_remove: "*" interval
_component: interval_to_add | interval_to_remove
components: _component ("," _component)*
NATURAL: "A" | "B" | "C" | "D" | "E" | "F" | "G"
MODIFIER: "b" | "#"
SHORTHAND: "minmaj7" | "maj7" | "min7" | "dim7" | "hdim7" 
            | "maj6" | "min6" | "maj9" | "min9" | "sus2" | "sus4" 
            | "maj" | "min" | "dim" | "aug" | "7" | "9" | "1" | "5"
            | "maj11" | "11" | "min11" | "maj13" | "13" | "min13"
NO_CHORD: "N" | "X"
NON_ZERO_DIGIT: "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
DIGIT: "0" | NON_ZERO_DIGIT
INTEGER: NON_ZERO_DIGIT DIGIT*
FLOAT: DIGIT+ "." DIGIT+ ("e" "-"? DIGIT+)?
_WHITES: /\s+/
